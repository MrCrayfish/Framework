plugins {
    id 'multiloader-common'
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
}

minecraft {
    version(minecraft_version)
    def aw = file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWideners(aw)
    }
    runs {
        client {
            client()
            displayName("Vanilla Client")
        }
        server {
            server()
            displayName("Vanilla Server")
        }
    }
}

dependencies {
    compileOnly "org.spongepowered:mixin:0.8.5"
    library "com.electronwill.night-config:core:3.7.1"
    library "com.electronwill.night-config:toml:3.7.1"
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}